-module(cmake_prv_clean).

-export([init/1, do/1, format_error/1]).

-define(NAMESPACE, cmake).
-define(PROVIDER, clean).
-define(DEPS, []).

-define(DEFAULT_BUILD_DIR, "c_src").

%% ===================================================================
%% Public API
%% ===================================================================
-spec init(rebar_state:t()) -> {ok, rebar_state:t()}.
init(State) ->
  Provider = providers:create([
            {name, ?PROVIDER},            % The 'user friendly' name of the task
            {module, ?MODULE},            % The module implementation of the task
            {bare, true},                 % The task can be run by the user, always true
            {deps, ?DEPS},                % The list of dependencies
            {example, "rebar3 cmake clean"}, % How to use the plugin
            {opts, []},                   % list of options understood by the plugin
            {short_desc, "Clean CMake project"},
            {desc, ""},
            {namespace, cmake}
    ]),
  {ok, rebar_state:add_provider(State, Provider)}.


-spec do(rebar_state:t()) -> {ok, rebar_state:t()} | {error, string()}.
do(State) ->
  Apps = case rebar_state:current_app(State) of
           undefined ->
             rebar_state:project_apps(State);
           AppInfo ->
             [AppInfo]
         end,

  lists:foreach(fun(App) ->
                    clean(App, State)
                end, Apps),
  
  {ok, State}.

-spec format_error(any()) ->  iolist().
format_error(Reason) ->
    io_lib:format("~p", [Reason]).


%% ===================================================================
%% Internal functions
%% ===================================================================
clean(AppInfo, _State) ->
  AppOutDir = rebar_app_info:out_dir(AppInfo),

  Opts = rebar_app_info:opts(AppInfo),
  CMakeOpts =
    case dict:find(cmake_opts, Opts) of
      {ok, CMakeOpts1} -> CMakeOpts1;
      error -> []
    end,
      
  BuildDir = filename:join([AppOutDir,
                            proplists:get_value(c_build, CMakeOpts,
                                                ?DEFAULT_BUILD_DIR)]),
  MakeCmd = proplists:get_value(make_cmd, CMakeOpts, "make"),
  Target = proplists:get_value(clean_target, CMakeOpts, "clean"),

  % Files generated by CMake itself will not be removed unless the
  % generated make clean target does that.
  case rebar_utils:sh([MakeCmd, " -C", BuildDir, " ", Target], [return_on_error]) of
    {error, {_, MakeError}} ->
      rebar_api:error("~s", [MakeError]),
      rebar_api:abort();
    {ok, MakeOutput} ->
      rebar_api:info("~s", [MakeOutput])
    end.
